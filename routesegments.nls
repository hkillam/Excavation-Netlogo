globals [
  road-to-entrance
  road-to-top-ramp
  road-to-top-nonramp
  road-down-ramp
  road-to-loader-A
  road-to-loader-B
  road-to-bottom-ramp
  road-to-exit-non-ramp
  road-up-ramp
  road-to-exit
  road-to-dump

  ;; mark some specific locations in our site
  dest-entrance
  dest-site-exit-A
  dest-site-exit-B
  dest-top-ramp
  dest-top-nonramp
  dest-pit-exit-A
  dest-pit-exit-B
  
]

to set-route-constants
  set road-to-entrance   1
  set road-to-top-ramp   2   ;; from entrance flagman to top of ramp
  set road-down-ramp     3   ;; from top of ramp to bottom
  set road-to-loader-A   4   ;;  to a piece of material
  set road-to-loader-B   5   ;;  to a piece of material
  set road-to-bottom-ramp 6  ;;  grab material and go to to bottom of ramp
  set road-up-ramp       7   ;;  up the ramp
  set road-to-exit      8   ;;  from ramp to exit flagman
  set road-to-dump      9   ;;  drive away
  set road-to-top-nonramp 10  ;; not using a ramp, turn right here.
  set road-to-exit-non-ramp 11;

  set dest-entrance patch  -11 5       ;; flagman at the entrance to the site
  set dest-site-exit-A patch 12 5
  set dest-site-exit-B patch 12 -7
  set dest-top-ramp patch 0 5
  set dest-top-nonramp patch -5 5          ;; go in a little before the old ramp location
  set dest-pit-exit-A patch 5 5            ;; at top of pit
  set dest-pit-exit-B patch 11 -7          ;; down side of pit
end


to-report ramp-ready?
  let ramp-ahead? true

  ;; not using the ramp, report that it is ready
  if use-ramp? = false [report true]
  
  ;; if this isn't a ramp, just return true
  ask patch-ahead 1 [
    if is-ramp? = false [
      set ramp-ahead? false
    ]
  ]
  if ramp-ahead? = false [
    set using-ramp? false
    report true
  ]

  if using-ramp? = true [
    report true  ;; I'm walking here
  ]

  ;; if the ramp is free, take it, and mark the direction it is being used.
  if count trucks with [using-ramp? = true] = 0 [
    set using-ramp? true
    
    ifelse road = road-to-bottom-ramp  or road = road-up-ramp [
       set ramp-down? false
       set ramp-up? true
    ][
      if road = road-to-top-ramp [
         set ramp-up? false
         set ramp-down? true  
      ]     
    ]
    report true
  ]
  
  ;; if the ramp is in use the same direction, jump on
  if  (road = road-to-bottom-ramp or road = road-up-ramp) and ramp-up? = true [
    set using-ramp? true
    report true
  ]
  
    ;; if the ramp is in use the same direction, jump on
  if  road = road-to-top-ramp and ramp-down? = true [
    set using-ramp? true
    report true
  ]

  report false
end

to set-next-section
    ifelse road = road-to-entrance [
      ifelse use-ramp? = true [
         set road road-to-top-ramp   ;; from entrance flagman to top of ramp
         set target dest-top-ramp
      ][
         set road road-to-top-nonramp
         set target dest-top-nonramp

      ]
    ][
    ifelse road = road-to-top-ramp [
      set road road-down-ramp   ;; from top of ramp to bottom
      set target patch 0 -4

    ][
    ifelse road = road-down-ramp or road = road-to-top-nonramp [
      set road road-to-loader-A  ;;  to a piece of material
      let target-loader one-of loaders with [group = 1  and material-remaining > 0]
      if contaminated? = true [
          set road road-to-loader-B
          set target-loader one-of loaders with [group = 2 and material-remaining > 0]
        ]
      set using-ramp? false
      rt 90
      fd 1
      set target [ patch-here ] OF target-loader
    ][
    ifelse road = road-to-loader-A or road = road-to-loader-B [
      set loading? true
      ifelse use-ramp? = true [
          set road road-to-bottom-ramp  ;;  grab material and go to to bottom of ramp
          set target patch 0 -5
      ][
          ;; else no ramp
          set road road-to-exit-non-ramp
          ifelse non-ramp-exit = "A"
          [
            set target dest-pit-exit-A
            ask flagmen with [id = 2] [  setxy 12 7 ]
         ]
         [  set target dest-pit-exit-B
            ask flagmen with [id = 2] [  setxy 12 6 ]
         ]
      ]

    ][
    ifelse road = road-to-bottom-ramp [

      set road road-up-ramp  ;;  up the ramp
      set target dest-top-ramp
    ][
    ifelse road = road-up-ramp [
      set road road-to-exit  ;;  from ramp to exit flagman
      set target dest-site-exit-A
    ][
    ifelse road = road-to-exit-non-ramp [
      set road road-to-exit  ;;  from ramp to exit flagman
      ifelse non-ramp-exit = "A" [  
         set target dest-site-exit-A
      ][  set target dest-site-exit-B ]          

    ][
    ifelse road = road-to-exit [
      set road road-to-dump  ;;  drive away
      set target patch 12 -13
      
    ][
    if road = road-to-dump [
      set road road-to-entrance  ;;  teleport back to beginning
      setxy -11 -15
      set target dest-entrance
      set loaded 0
      ifelse contaminated? = true [
         set take-a-dump random-normal cont-time-unload cont-time-unload-deviation     
         if cont-material-remaining <= 0 [die]
      ][
         set take-a-dump random-normal time-unload time-unload-deviation
         if material-remaining <= 0 [die]
      ]
  ]]]]]]]]]

    face-nowrap target
end
